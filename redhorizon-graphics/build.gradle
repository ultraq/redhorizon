/*
 * Copyright 2025, Emanuel Rabina (http://www.ultraq.net.nz/)
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
	id 'redhorizon-build'
}

description = 'Graphics module for the Red Horizon project'

// @formatter:off
configure {
	asGroovyLibraryProject()
		.withGroovydocOptions() {
			link("https://javadoc.io/doc/org.joml/joml/${jomlVersion}", 'org.joml.')
			link("https://javadoc.io/doc/org.joml.primitives/joml-primitives/${jomlPrimitivesVersion}", 'org.joml.primitives.')
			link("https://javadoc.io/doc/io.github.spair/imgui-java-binding/${imguiVersion}", 'imgui.')
			link("https://javadoc.io/doc/nz.net.ultraq/event-horizon/${eventHorizonVersion}/", 'nz.net.ultraq.eventhorizon.')
			link("https://javadoc.io/doc/nz.net.ultraq/redhorizon-input/${version}/", 'nz.net.ultraq.redhorizon.input.')
			link("https://javadoc.io/doc/nz.net.ultraq/redhorizon-scenegraph/${version}/", 'nz.net.ultraq.redhorizon.scenegraph.')
		}
		.withJarOptions() {
			manifest {
				attributes 'Automatic-Module-Name': 'nz.net.ultraq.redhorizon.graphics'
			}
		}
		.withSourcesJar()
		.withGroovydocJar()
		.configureSource()
			.withDependencies() {
				implementation project(':redhorizon-audio')
				implementation project(':redhorizon-input')
				implementation project(':redhorizon-scenegraph')
				implementation "nz.net.ultraq.groovy:joml-extensions:${jomlExtensionsVersion}"
				implementation "io.github.spair:imgui-java-binding:${imguiVersion}"
				implementation "io.github.spair:imgui-java-lwjgl3:${imguiVersion}"
				implementation "org.joml:joml:${jomlVersion}"
				implementation "org.joml:joml-primitives:${jomlPrimitivesVersion}"

				api "nz.net.ultraq:event-horizon:${eventHorizonVersion}"
				api platform("org.lwjgl:lwjgl-bom:${lwjglVersion}")
				api 'org.lwjgl:lwjgl'
				api 'org.lwjgl:lwjgl-glfw'
				api 'org.lwjgl:lwjgl-opengl'

				runtimeOnly "io.github.spair:imgui-java-natives-macos:${imguiVersion}"
				runtimeOnly "io.github.spair:imgui-java-natives-windows:${imguiVersion}"
				['natives-macos-arm64', 'natives-windows'].each { natives ->
					runtimeOnly "org.lwjgl:lwjgl::${natives}"
					runtimeOnly "org.lwjgl:lwjgl-glfw::${natives}"
					runtimeOnly "org.lwjgl:lwjgl-opengl::${natives}"
				}
			}
			.expandExtensionModuleVersion()
		.configureVerification()
			.withTestDependencies() {
				testImplementation "ch.qos.logback:logback-classic:${logbackVersion}"
			}
}
// @formatter:on
