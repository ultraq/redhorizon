/* 
 * Copyright 2015, Emanuel Rabina (http://www.ultraq.net.nz/)
 * 
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 * 
 *     http://www.apache.org/licenses/LICENSE-2.0
 * 
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
	id 'maven-publish'
	id 'com.github.johnrengelman.shadow' version '8.1.1'
}
apply from: 'https://raw.githubusercontent.com/ultraq/gradle-support/4.3.1/maven-publish-support.gradle'

description = 'Code that bridges the classic C&C file formats for use with the Red Horizon engine'
year = '2007'

dependencies {
	implementation project(':redhorizon-async')
	implementation project(':redhorizon-engine')
	implementation project(':redhorizon-events')
	implementation project(':redhorizon-filetypes')
	implementation "org.joml:joml-primitives:${jomlPrimitivesVersion}"
}

shadowJar {
	archiveClassifier.set('')
	dependencies {
		include(project(':redhorizon-async'))
		include(project(':redhorizon-engine'))
		include(project(':redhorizon-events'))
		include(project(':redhorizon-filetypes'))
	}
}

groovydoc {
	overviewText = resources.text.fromString('''
		A module that bridges the classic C&C file formats for use with the Red Horizon
		engine.  All of the reverse engineering of the old codecs and files takes place
		here, and serve as working examples for other programmers.
	''')
	// Can't link to these until they get published on maven central themselves
//	link("https://javadoc.io/doc/nz.net.ultraq.redhorizon/redhorizon-async/${version}/", 'nz.net.ultraq.redhorizon.async.')
//	link("https://javadoc.io/doc/nz.net.ultraq.redhorizon/redhorizon-events/${version}/", 'nz.net.ultraq.redhorizon.events.')
//	link("https://javadoc.io/doc/nz.net.ultraq.redhorizon/redhorizon-filetypes/${version}/", 'nz.net.ultraq.redhorizon.filetypes.')
}

publishing {
	publications {
		library(MavenPublication) {
			artifacts = [shadowJar, sourcesJar, groovydocJar]
			afterEvaluate {
				// Have to manually remove the project nodes since shadow won't do that for us
				pom.withXml {
					var projectDependencyNodes = []
					asNode().depthFirst { node ->
						if (node.name().localPart == 'dependency' && node.get('groupId').text() == 'nz.net.ultraq.redhorizon') {
							projectDependencyNodes << node
						}
					}
					projectDependencyNodes.each { node ->
						node.parent().remove(node)
					}
				}
			}
		}
	}
}

// Disable module metadata while publishing fat jars - it's incorrect for what
// we're shipping
tasks.withType(GenerateModuleMetadata) {
	enabled = false
}
