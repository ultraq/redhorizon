/* 
 * Copyright 2016, Emanuel Rabina (http://www.ultraq.net.nz/)
 * 
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 * 
 *     http://www.apache.org/licenses/LICENSE-2.0
 * 
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

description = 'Media player for the Red Horizon project'
archivesBaseName = 'redhorizon-utilities-mediaplayer'

dependencies {
	implementation(
		project(':redhorizon-classic'),
		project(':redhorizon-engine'),
		project(':redhorizon-events'),
		project(':redhorizon-extensions'),
		project(':redhorizon-filetypes'),
		'org.joml:joml:1.9.19',
		'org.reflections:reflections:0.9.11'
	)
	implementation('nz.net.ultraq.extensions:groovy-extensions:0.3.0') {
		exclude module: 'groovy'
	}
}

application {
	mainClassName = 'nz.net.ultraq.redhorizon.MediaPlayer'
	applicationName = 'play'
}

startScripts {
	unixStartScriptGenerator = new CustomUnixStartScriptGenerator(generator: unixStartScriptGenerator)
}

class CustomUnixStartScriptGenerator implements ScriptGenerator {
	@Delegate
	ScriptGenerator generator
	@Override
	void generateScript(JavaAppStartScriptGenerationDetails details, Writer writer) {
		details.defaultJvmOpts.add(0, '-XstartOnFirstThread')
		generator.generateScript(details, writer)
	}
}
