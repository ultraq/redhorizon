/*
 * Copyright 2020, Emanuel Rabina (http://www.ultraq.net.nz/)
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
	id 'application'
}

def picocliVersion = '4.7.5'

dependencies {
	implementation project(':redhorizon-classic')
	implementation project(':redhorizon-engine')
	implementation project(':redhorizon-events')
	implementation project(':redhorizon-explorer')
	implementation project(':redhorizon-filetypes')
	implementation project(':redhorizon-sandbox')
	implementation project(':redhorizon-shooter')
	implementation platform("org.lwjgl:lwjgl-bom:${lwjglVersion}")
	implementation "ch.qos.logback:logback-core:${logbackVersion}"
	implementation "info.picocli:picocli:${picocliVersion}"
	implementation 'nz.net.ultraq.preferences:preferences:3.1.0'
	implementation "org.apache.groovy:groovy-json:${groovyVersion}"
	implementation "org.joml:joml:${jomlVersion}"
	implementation "org.joml:joml-primitives:${jomlPrimitivesVersion}"
	implementation 'org.lwjgl:lwjgl-glfw'
	runtimeOnly "ch.qos.logback:logback-classic:${logbackVersion}"

	testImplementation 'com.github.valfirst:slf4j-test:3.0.1'
	testImplementation 'org.assertj:assertj-core:3.26.3'
}

configurations {
	testRuntimeOnly {
		exclude group: 'ch.qos.logback'
	}
}

processResources {
	filesMatching('**/cli.properties') {
		expand version: version
	}
}

compileJava {
	options.compilerArgs += ["-Aproject=${project.group}/${project.name}"]
}

def args = [
	'-Dorg.lwjgl.system.stackSize=10240'
]
if (targetPlatform.startsWith('macos')) {
	args << '-Dorg.lwjgl.glfw.libname=glfw_async'
}

application {
	mainClass = 'nz.net.ultraq.redhorizon.cli.RedHorizon'
	applicationName = 'redhorizon'
	applicationDefaultJvmArgs = args
}

distributions {
	main {
		contents {
			// Have to mention explicitly because of the subproject layout
			from('..') {
				include 'CHANGELOG.md'
				include 'LICENSE.txt'
				include 'README.md'
			}
		}
	}
}

distTar {
	enabled = false
}
