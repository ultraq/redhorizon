/* 
 * Copyright 2007, Emanuel Rabina (http://www.ultraq.net.nz/)
 * 
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 * 
 *     http://www.apache.org/licenses/LICENSE-2.0
 * 
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
	id 'distribution'
}

// Global configuration
allprojects {
	group = 'nz.net.ultraq.redhorizon'
	version = '0.31.0'
}

// Root project configuration
description = 'Recreating the original 2D Command & Conquer games'

distributions {
	main {
		contents {
			from 'redhorizon-utilities/converter/build/install/convert'
			from 'redhorizon-utilities/mediaplayer/build/install/play'
			from 'redhorizon-utilities/mixreader/build/install/mix'
			from 'redhorizon-utilities/unitviewer/build/install/view'
			from('.') {
				include 'CHANGELOG.md'
				include 'LICENSE.txt'
				include 'README.md'
			}
		}
	}
}

[distTar, distZip].each {
	it.dependsOn = [
		':redhorizon-utilities:converter:installDist',
		':redhorizon-utilities:mediaplayer:installDist',
		':redhorizon-utilities:mixreader:installDist',
		':redhorizon-utilities:unitviewer:installDist'
	]
	it.duplicatesStrategy = 'exclude'
}

// Subproject configuration
subprojects {
	apply plugin: 'groovy'
	apply plugin: 'idea'
	apply from: 'https://raw.githubusercontent.com/ultraq/gradle-support/3.1.2/gradle-support.gradle'

	sourceCompatibility = 11

	repositories {
		flatDir {
			dirs "$rootDir/libraries"
		}
	}

	dependencies {
		implementation(
			'org.codehaus.groovy:groovy:3.0.7:indy',
			'org.slf4j:slf4j-api:1.7.30'
		)
		implementation('nz.net.ultraq.extensions:profiling-extensions:0.5.0') {
			exclude module: 'groovy'
		}
		testImplementation('org.spockframework:spock-core:2.0-M4-groovy-3.0') {
			exclude group: 'org.codehaus.groovy'
		}
	}

	compileGroovy {
		groovyOptions.optimizationOptions.indy = true
	}

	test {
		useJUnitPlatform()
	}
}
