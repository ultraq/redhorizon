/* 
 * Copyright 2015, Emanuel Rabina (http://www.ultraq.net.nz/)
 * 
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 * 
 *     http://www.apache.org/licenses/LICENSE-2.0
 * 
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import org.apache.tools.ant.taskdefs.condition.Os

description = 'Filetypes module for the Red Horizon project'

dependencies {
	implementation(
		project(':redhorizon-codecs'),
		project(':redhorizon-extensions'),
		':blowfishj:2.41'
	)
	implementation('nz.net.ultraq.extensions:profiling-extensions:0.3.0') {
		exclude module: 'groovy'
	}
}

processResources {
	expand([
		moduleVersion: version
	])
}

def isMac = Os.isFamily(Os.FAMILY_MAC)
def isUnix = Os.isFamily(Os.FAMILY_UNIX)
def isWindows = Os.isFamily(Os.FAMILY_WINDOWS)

task compileMixFileKey(type: Exec) {
	onlyIf {
		return !(
			isMac ? file('libMixFileKey.dylib') :
			isUnix ? file('libMixFileKey.so') :
			isWindows ? file('MixFileKey.dll') :
			true
		)
	}

	if (isUnix) {
		commandLine 'make', isMac ? 'macos' : 'linux'
	}
	else if (isWindows) {
		commandLine 'nmake', 'windows'
	}
	else {
		doLast {
			println 'No C++ build configuration for this platform'
		}
	}
}

build {
	dependsOn 'compileMixFileKey'
}
