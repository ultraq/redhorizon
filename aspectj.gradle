/**
 * Copied from http://www.breskeby.com/2010/02/using-gradle-with-aspectj/
 * It didn't work straight off for me, so below are just the modifications I
 * needed for my own projects.
 */

configurations {
	ajc
	aspects
	ajInpath
}

dependencies {
	ajc 'org.aspectj:aspectjtools:1.7.3'
	compile 'org.aspectj:aspectjrt:1.7.3'
}

sourceSets {
	main {
		resources {
			exclude '**/*.aj'
		}
	}
}

/**
 * Replace the standard compile action with that of the IAJC Ant task.
 */
compileJava.deleteAllActions()
compileJava << {
	ant.taskdef(
		resource: 'org/aspectj/tools/ant/taskdefs/aspectjTaskdefs.properties',
		classpath: configurations.ajc.asPath)
	ant.iajc(
		source: sourceCompatibility,
		target: targetCompatibility,
		destDir: sourceSets.main.output.classesDir.absolutePath,
		fork: 'true',
		X: 'noInline',
		aspectPath: configurations.aspects.asPath,
		inpath: configurations.ajInpath.asPath,
		sourceRootCopyFilter: '**/*.java,**/*.aj',
		classpath: configurations.compile.asPath) {

		sourceroots {
			sourceSets.main.java.srcDirs.each { dir ->
				pathelement(location: dir.absolutePath)
			}
		}
	}
}
