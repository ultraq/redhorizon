/* 
 * Copyright 2021, Emanuel Rabina (http://www.ultraq.net.nz/)
 * 
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 * 
 *     http://www.apache.org/licenses/LICENSE-2.0
 * 
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
	id 'application'
}

dependencies {
	implementation(
		project(':redhorizon-classic'),
		project(':redhorizon-engine'),
		project(':redhorizon-events'),
		project(':redhorizon-extensions'),
		project(':redhorizon-filetypes'),
		platform("org.lwjgl:lwjgl-bom:${lwjglVersion}"),
		"ch.qos.logback:logback-classic:${logbackVersion}",
		"info.picocli:picocli:${picocliVersion}",
		"io.github.spair:imgui-java-binding:${imguiVersion}",
		"io.github.spair:imgui-java-lwjgl3:${imguiVersion}",
		'nz.net.ultraq.preferences:preferences:3.0.0',
		"org.codehaus.groovy:groovy-json:${groovyVersion}:indy",
		"org.joml:joml:${jomlVersion}",
		"org.joml:joml-primitives:${jomlPrimitivesVersion}",
		'org.lwjgl:lwjgl',
		'org.lwjgl:lwjgl-glfw'
	)
}

application {
	mainClass = 'nz.net.ultraq.redhorizon.explorer.ExplorerCli'
	applicationName = 'explorer'
	applicationDefaultJvmArgs = [
		"-Dredhorizon.version=${version}",
		'-Dorg.lwjgl.system.stackSize=10240'
	]
}

startScripts {
	unixStartScriptGenerator = new CustomUnixStartScriptGenerator(generator: unixStartScriptGenerator)
}

class CustomUnixStartScriptGenerator implements ScriptGenerator {
	@Delegate
	ScriptGenerator generator
	@Override
	void generateScript(JavaAppStartScriptGenerationDetails details, Writer writer) {
		details.defaultJvmOpts.add(0, '-XstartOnFirstThread')
		generator.generateScript(details, writer)
	}
}
