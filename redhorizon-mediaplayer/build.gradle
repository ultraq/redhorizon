/* 
 * Copyright 2016, Emanuel Rabina (http://www.ultraq.net.nz/)
 * 
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 * 
 *     http://www.apache.org/licenses/LICENSE-2.0
 * 
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
	id 'application'
}

description = 'Media player for the Red Horizon project'

dependencies {
	implementation(
		project(':redhorizon-engine'),
		project(':redhorizon-extensions'),
		project(':redhorizon-filetypes'),
		'ch.qos.logback:logback-classic:1.2.3',
		'info.picocli:picocli:4.1.1',
		'org.joml:joml:1.9.19',
		'org.reflections:reflections:0.9.11'
	)
}

application {
	mainClassName = 'nz.net.ultraq.redhorizon.MediaPlayer'
	applicationName = 'play'
	applicationDefaultJvmArgs = [
		'-Djava.library.path=./lib',
		"-Dredhorizon.version=${version}"
	] + [
		'java.io',
		'java.math',
		'java.lang',
		'java.lang.annotation',
		'java.lang.invoke',
		'java.lang.reflect',
		'java.nio',
		'java.util',
		'java.util.concurrent',
		'java.util.regex',
		'sun.nio.ch'
	].inject([]) { acc, javaPackage ->
		acc << '--add-opens' << "java.base/$javaPackage=ALL-UNNAMED"
	}
}

distributions {
	main {
		contents {
			from('../redhorizon-filetypes') {
				include '*MixFileKey*'
				into 'lib'
			}
		}
	}
}

startScripts {
	unixStartScriptGenerator = new CustomUnixStartScriptGenerator(generator: unixStartScriptGenerator)
}

class CustomUnixStartScriptGenerator implements ScriptGenerator {
	@Delegate
	ScriptGenerator generator
	@Override
	void generateScript(JavaAppStartScriptGenerationDetails details, Writer writer) {
		details.defaultJvmOpts.add(0, '-XstartOnFirstThread')
		generator.generateScript(details, writer)
	}
}
