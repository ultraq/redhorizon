/* 
 * Copyright 2007, Emanuel Rabina (http://www.ultraq.net.nz/)
 * 
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 * 
 *     http://www.apache.org/licenses/LICENSE-2.0
 * 
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import org.gradle.internal.os.OperatingSystem

def os = OperatingSystem.current()
def targetPlatform = findProperty('targetPlatform') ?:
	os == OperatingSystem.MAC_OS ? System.getProperty('os.arch').startsWith('aarch64') ? 'macos-arm64' : 'macos' : 'windows'

// Global configuration
allprojects {
	group = 'nz.net.ultraq.redhorizon'
	version = '0.35.0'
}

// Root project configuration
description = 'Recreating the original 2D Command & Conquer games'

// Subproject configuration
subprojects {
	apply plugin: 'java-library'
	apply plugin: 'groovy'
	apply plugin: 'idea'
	apply from: 'https://raw.githubusercontent.com/ultraq/gradle-support/4.3.1/gradle-support.gradle'

//	configurations.all {
//		resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
//		resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
//	}

	ext {
		groovyVersion = '4.0.7'
		imguiVersion = '1.86.4'
		jomlVersion = '1.10.5'
		jomlPrimitivesVersion = '1.10.0'
		logbackVersion = '1.4.5'
		lwjglVersion = '3.3.1'
		slf4jVersion = '2.0.6'
	}
	ext.targetPlatform = targetPlatform

	dependencies {
		implementation 'nz.net.ultraq.groovy:groovy-extensions:2.1.0'
		implementation 'nz.net.ultraq.groovy:groovy-io-extensions:0.1.2'
		implementation 'nz.net.ultraq.groovy:groovy-profiling-extensions:0.7.0'
		implementation "org.apache.groovy:groovy:${groovyVersion}"
		implementation "org.slf4j:slf4j-api:${slf4jVersion}"

		testImplementation 'org.spockframework:spock-core:2.3-groovy-4.0'
		testRuntimeOnly "org.slf4j:slf4j-nop:${slf4jVersion}"
	}

	java {
		toolchain {
			languageVersion = JavaLanguageVersion.of(17)
			vendor = JvmVendorSpec.ADOPTIUM
		}
	}

	test {
		useJUnitPlatform()
	}
}
