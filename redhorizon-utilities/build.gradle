/* 
 * Copyright 2020, Emanuel Rabina (http://www.ultraq.net.nz/)
 * 
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 * 
 *     http://www.apache.org/licenses/LICENSE-2.0
 * 
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

subprojects {
	apply plugin: 'application'

	dependencies {
		implementation(
			'ch.qos.logback:logback-classic:1.2.3',
			'info.picocli:picocli:4.1.1'
		)
	}

	application {
		applicationDefaultJvmArgs = [
			"-Dredhorizon.version=${version}",
			'-Dorg.lwjgl.system.stackSize=10240'
		]
	}

	startScripts {
		unixStartScriptGenerator = new CustomUnixStartScriptGenerator(generator: unixStartScriptGenerator)
	}
}

class CustomUnixStartScriptGenerator implements ScriptGenerator {
	@Delegate
	ScriptGenerator generator
	@Override
	void generateScript(JavaAppStartScriptGenerationDetails details, Writer writer) {
		details.defaultJvmOpts.add(0, '-XstartOnFirstThread')
		generator.generateScript(details, writer)
	}
}
